#!/usr/bin/perl -w
# i-scream libstatgrab
# http://www.i-scream.org
# Copyright (C) 2000-2011 i-scream
# 
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
#
# $Id$

use strict;
use Getopt::Long;

my $progname = "statgrab-make-mrtg-config";

my $package_version = '@PACKAGE_VERSION@';
my $package_bugreport = '@PACKAGE_BUGREPORT@';
my $help_text = <<EOF;
Usage: $progname [OPTION]... [CONFIGFILE]...
Generate an XHTML index page on stdout from MRTG config files specified
on the command line or read from stdin.

--title TITLE                Use TITLE as the title of the generated page
--help                       Display this help and exit

Version $package_version - report bugs to $package_bugreport.
EOF

sub main () {
	my $hostname = `hostname`;
	chomp $hostname;
	my $pagetitle = "MRTG: $hostname";

	GetOptions('title=s' => \$pagetitle,
	           'help' => \my $help) or die $help_text;
	if ($help) {
		print "$help_text";
		exit 0;
	}

	my %pages = ();
	while (<>) {
		/^Title\[([^\]]+)\]: (.*)$/ or next;
		$pages{$2} = $1;
	}
	print <<EOF;
<?xml version="1.0" encoding="ISO-8859-1"?>
<!DOCTYPE html
    PUBLIC "-//W3C//DTD XHTML 1.1//EN"
    "http://www.w3.org/TR/xhtml11/DTD/xhtml11.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en">
<head>
    <title>$pagetitle</title>
</head>
<body>
<h1>$pagetitle</h1>
EOF
	foreach my $title (sort keys %pages) {
		my $page = $pages{$title};
		print "<h2><a href=\"$page.html\">$title</a></h2>\n";
		print "<p><img src=\"$page-day.png\" alt=\"$page\" /></p>\n";
	}
	print <<EOF;
<p>Generated by <a href="http://www.i-scream.org/libstatgrab">
libstatgrab</a> version $package_version.</p>
</body>
</html>
EOF
}

main();
